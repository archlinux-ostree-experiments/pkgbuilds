# Maintainer: Marco Huenseler <marco@wuthoehle.de>

pkgname=bootupd-arch-git
_pkgname=bootupd
pkgver=0.2.28.r21.g1645ef3
pkgrel=2
pkgdesc="Bootloader updater"
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url="https://github.com/archlinux-ostree-experiments/bootupd"
license=('Apache-2.0')
depends=(gcc-libs
         glibc
         efibootmgr)
makedepends=(cargo
             git)
provides=("bootupd=$pkgver")
conflicts=('bootupd')
source=("git+$url.git")
sha256sums=('SKIP')
# options=(!lto)

prepare() {
  cd "$_pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

pkgver() {
  cd "$_pkgname"
  git describe --long --tags --abbrev=7 --match="v*" HEAD |
    sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  make
}

package() {
  cd "$_pkgname"
  make DESTDIR="$pkgdir" install
  install -Dm644 systemd/bootloader-update.service -t "${pkgdir}/usr/lib/systemd/system/"
}
